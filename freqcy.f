      SUBROUTINE FREQCY(FMATRX,FREQ,CNORML,REDMAS)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES'
      DIMENSION FMATRX(*), REDMAS(*), FREQ(*), CNORML(*)
*********************************************************************
*
*  FRCE CALCULATES THE FORCE CONSTANTS AND VIBRATIONAL FREQUENCIES 
*       FOR A MOLECULE.  IT USES THE ISOTOPIC MASSES TO WEIGHT THE
*       FORCE MATRIX
*
* ON INPUT   FMATRX   =  FORCE MATRIX, OF SIZE NUMAT*3*(NUMAT*3+1)/2.
*
*********************************************************************
      COMMON /MOLKST/ NUMAT,NAT(NUMATM),NFIRST(NUMATM),NMIDLE(NUMATM),
     1                NLAST(NUMATM), NORBS, NELECS,NALPHA,NBETA,
     +                NCLOSE,NOPEN
      COMMON /ATMASS/ ATMASS(NUMATM)
      COMMON /SCRACH/ OLDF(MAXPAR)
      DIMENSION WTMASS(MAXPAR)
      DATA FACT/6.023D23/
C
C    CONVERSION FACTOR FOR SPEED OF LIGHT AND 2 PI.
C
      C2PI=1.D0/(2.998D10*3.141592653598D0*2.D0)
C NOW TO CALCULATE THE VIBRATIONAL FREQUENCIES
C
C   FIND CONVERSION CONSTANTS FOR MASS WEIGHTED SYSTEM
      N3=3*NUMAT
      L=0
       DO 50 I=1,NUMAT
          WEIGHT=2.D0/SQRT(ATMASS(I))
          DO 50 J=1,3
              L=L+1
  50          WTMASS(L)=WEIGHT
C    CONVERT TO MASS WEIGHTED FMATRX
      LINEAR=0
      DO 60 I=1,N3
         DO 60 J=1,I
           LINEAR=LINEAR+1
           OLDF(LINEAR)=  FMATRX(LINEAR)*1.D5
  60       FMATRX(LINEAR)=FMATRX(LINEAR)*WTMASS(I)*WTMASS(J)*1.D5
C
C    1.D5 IS TO CONVERT FROM MILLIDYNES/ANGSTROM TO DYNES/CM.
C
C    DIAGONALIZE
                CALL HQRII(FMATRX,N3,N3,FREQ,CNORML)
C
C    CALCULATE REDUCED MASSES, STORE IN REDMAS
C
      DO 65 I=1,N3
          II=(I-1)*N3
          SUM=0.D0
          DO 66 J=1,N3
              JII=J+II
              JJ=(J*(J-1))/2
              DO 67 K=1,J
  67             SUM=SUM+CNORML(JII)*OLDF(JJ+K)*CNORML(K+II)
              DO 68 K=J+1,N3
  68             SUM=SUM+CNORML(JII)*OLDF((K*(K-1))/2+J)*CNORML(K+II)
  66          CONTINUE
          SUM=SUM/FREQ(I)
          IF(I.LT.8.AND.(SUM.LT.0.D0.OR.SUM.GT.100))SUM=0.D0
  65      REDMAS(I)=SUM
C
C    SWITCH EIGENVALUES TO FREQUENCIES
      DO 61 I=1,N3
  61     FREQ(I)=SQRT(FACT*ABS(FREQ(I)))*C2PI
C
C    CONVERT NORMAL VECTORS TO CARTESIAN COORDINATES
      IJ=0
      DO 62 I=1,N3
      DO 62 J=1,N3
      IJ=IJ+1
  62  CNORML(IJ)=CNORML(IJ)*WTMASS(J)
C  62  CNORML(IJ)=CNORML(IJ)/WTMASS(J)
      RETURN
      END
