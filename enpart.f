      SUBROUTINE ENPART(UHF,H,ALPHA,BETA,P,COORD)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'SIZES/NOLIST'
      PARAMETER (NATMS2 = (NUMATM*(NUMATM+1))/2)
      DIMENSION H(*), ALPHA(*), BETA(*), P(*), COORD(3,*)
      LOGICAL UHF
C*************************************************************************
C
C *** ENERGY PARTITIONIG WITHIN THE UMNDO SCHEME
C     ROUTINE WRITTEN BY S.OLIVELLA, BARCELONA NOV. 1979.
C
C   ON INPUT UHF     = .TRUE. IF A U.H.F. CALCULATION.
C            H       = ONE-ELECTRON MATRIX.
C            ALPHA   = ALPHA ELECTRON DENSITY.
C            BETA    = BETA ELECTRON DENSITY.
C            P       = TOTAL ELECTRON DENSITY.
C 
C    NOTHING IS CHANGED ON EXIT.
C
C*************************************************************************
      COMMON /ONELEC/ USS(54), UPP(54), UDD(54)
      COMMON /CORE  / CORE(54)
      COMMON /ALPHA / ALP(54)
      COMMON /TWOELE/ GSS(54),GSP(54),GPP(54),GP2(54),HSP(54)
      COMMON /MOLKST/ NUMAT,NAT(NUMATM),NFIRST(NUMATM),NMIDLE(NUMATM)
     +               ,NLAST(NUMATM),NORBS,NELECS,
     1                NALPHA,NBETA,NCLOSE,NOPEN
      COMMON /WMATRX/ W(N2ELEC)
      PARAMETER(MDUMY=MAXORB*MAXORB+(NUMATM*(NUMATM+1))/2-
     +NUMATM*3-NATMS2*4)
      COMMON /SCRACH/ EA(NUMATM,2),EAT(NUMATM), E(NATMS2,4),
     +XDUMY(MDUMY)
C
C *** RECALCULATE THE DENSITY MATRICES IN THE UHF SCHEME
C
      LINEAR=NORBS*(NORBS+1)/2
      IF( .NOT. UHF) THEN
          DO 1 I=1,LINEAR
  1       BETA(I)=ALPHA(I)
      ENDIF
C
C *** ONE-CENTRE ENERGIES
      K=0
      DO 20 I=1,NUMAT
      IA=NFIRST(I)
      IB=NLAST(I)
      NI=NAT(I)
      EA(I,1)=0.0
      DO 10 J=IA,IB
      K=K+J
      T=UPP(NI)
      IF(J.EQ.IA) T=USS(NI)
 10   EA(I,1)=EA(I,1)+P(K)*T
      ISS=(IA*(IA+1))/2
      EA(I,2)=0.5*GSS(NI)*P(ISS)*P(ISS)
     +  -0.5*GSS(NI)*(ALPHA(ISS)*ALPHA(ISS)+BETA(ISS)*BETA(ISS))
      IF(IA.EQ.IB) GO TO 20
      IA1=IA+1
      IA2=IA+2
      IXX=IA1*IA2/2
      IYY=IA2*IB/2
      IZZ=(IB*(IB+1))/2
      IXY=IA1+IA2*IA1/2
      IXZ=IA1+IB*IA2/2
      IYZ=IA2+IB*IA2/2
      ISX=IA+IA1*IA/2
      ISY=IA+IA2*IA1/2
      ISZ=IA+IB*IA2/2
      SS1=P(IXX)*P(IXX)+P(IYY)*P(IYY)+P(IZZ)*P(IZZ)
      SS2=P(ISS)*(P(IXX)+P(IYY)+P(IZZ))
      SS3=P(IXX)*P(IYY)+P(IXX)*P(IZZ)+P(IYY)*P(IZZ)
      SS4=P(ISX)*P(ISX)+P(ISY)*P(ISY)+P(ISZ)*P(ISZ)
      SS5=P(IXY)*P(IXY)+P(IXZ)*P(IXZ)+P(IYZ)*P(IYZ)
      TT1=ALPHA(IXX)*ALPHA(IXX)+ALPHA(IYY)*ALPHA(IYY)
     ++ALPHA(IZZ)*ALPHA(IZZ)+BETA(IXX)*BETA(IXX)
     1+BETA(IYY)*BETA(IYY)+BETA(IZZ)*BETA(IZZ)
      TT2=ALPHA(ISS)*(ALPHA(IXX)+ALPHA(IYY)+ALPHA(IZZ))
     .   +BETA(ISS)*(BETA(IXX)+BETA(IYY)+BETA(IZZ))
      TT3=ALPHA(IXX)*ALPHA(IYY)+ALPHA(IXX)*ALPHA(IZZ)
     ++ALPHA(IYY)*ALPHA(IZZ)+BETA(IXX)*BETA(IYY)
     1+BETA(IXX)*BETA(IZZ)+BETA(IYY)*BETA(IZZ)
      TT4=ALPHA(ISX)*ALPHA(ISX)+ALPHA(ISY)*ALPHA(ISY)
     ++ALPHA(ISZ)*ALPHA(ISZ)+BETA(ISX)*BETA(ISX)
     1+BETA(ISY)*BETA(ISY)+BETA(ISZ)*BETA(ISZ)
      TT5=ALPHA(IXY)*ALPHA(IXY)+ALPHA(IXZ)*ALPHA(IXZ)
     ++ALPHA(IYZ)*ALPHA(IYZ)+BETA(IXY)*BETA(IXY)
     1+BETA(IXZ)*BETA(IXZ)+BETA(IYZ)*BETA(IYZ)
      EA(I,2)=EA(I,2)+0.5*GPP(NI)*SS1+GSP(NI)*SS2
     ++GP2(NI)*SS3+HSP(NI)*SS4*2.0+0.5D0*(GPP(NI)-GP2(NI))*SS5*2.0
     .                -0.5*GPP(NI)*TT1-GSP(NI)*TT4-GP2(NI)*TT5-
     .        HSP(NI)*(TT2+TT4)-0.5D0*(GPP(NI)-GP2(NI))*(TT3+TT5)
 20   CONTINUE
      WRITE(6,610)
 610  FORMAT(//,10X,'TOTAL ENERGY PARTITIONING IN MNDO')
      WRITE(6,620)
 620  FORMAT(/,' ONE-CENTRE ENERGIES (EV)')
      K=1
      KL=NUMAT
      IF(NUMAT.GT.10) KL=10
 30   WRITE(6,621)(I,I=K,KL)
 621  FORMAT(/,'  ATOM  ',10(I7,3X))
      WRITE(6,622)(EA(I,1),I=K,KL)
 622  FORMAT(/,'  EA U  ',10F10.3)
      WRITE(6,623)(EA(I,2),I=K,KL)
 623  FORMAT(/,'  EA E  ',10F10.3)
      DO 40 I=K,KL
 40   EAT(I)=EA(I,1)+EA(I,2)
      WRITE(6,625)(EAT(I),I=K,KL)
 625  FORMAT(/,'  TOTAL ',10F10.3)
      IF(NUMAT.LE.KL) GO TO 50
      K=KL+1
      KL=K+9
      GO TO 30
 50   EAU=0.0
      EAE=0.0
      DO 60 I=1,NUMAT
      NI=NAT(I)
      EAU=EAU+EA(I,1)
 60   EAE=EAE+EA(I,2)
      TONE=EAU+EAE
      EABE=0.0
      EABV=0.0
      EABN=0.0
      EABR=0.0
C *** TWO-CENTRE ENERGIES
C     RESONANCE TERMS
      N=1
      DO 80 II=2,NUMAT
      IA=NFIRST(II)
      IB=NLAST(II)
      IMINUS=II-1
      DO 70 JJ=1,IMINUS
      N=N+1
      JA=NFIRST(JJ)
      JB=NLAST(JJ)
      E(N,1)=0.0
      DO 70 I=IA,IB
      KA=(I*(I-1))/2
      DO 70 K=JA,JB
      IK=KA+K
 70   E(N,1)=E(N,1)+2.0*P(IK)*H(IK)
 80   N=N+1
C     CORE ATTRACTION AND CORE REPULSION TERMS
      N=1
      KK=0
      DO 150 II=2,NUMAT
      IA=NFIRST(II)
      IB=NLAST(II)
      NI=NAT(II)
      ISS=(IA*(IA+1))/2
      IMINUS=II-1
      DO 140 JJ=1,IMINUS
      N=N+1
      JA=NFIRST(JJ)
      JB=NLAST(JJ)
      NJ=NAT(JJ)
      JSS=(JA*(JA+1))/2
      KK=KK+1
      G=W(KK)
      R=SQRT((COORD(1,II)-COORD(1,JJ))**2+(COORD(2,II)-COORD(2,JJ))**2+
     .  (COORD(3,II)-COORD(3,JJ))**2)
      SCALE=1.0+EXP(-ALP(NI)*R)+EXP(-ALP(NJ)*R)
      NT=NI+NJ
      IF(NT.LT.8.OR.NT.GT.9) GO TO 90
      IF(NI.EQ.7.OR.NI.EQ.8) SCALE=SCALE+(R-1.0)*EXP(-ALP(NI)*R)
      IF(NJ.EQ.7.OR.NJ.EQ.8) SCALE=SCALE+(R-1.0)*EXP(-ALP(NJ)*R)
 90   E(N,2)=CORE(NI)*CORE(NJ)*G*SCALE
      E(N,3)=-(P(ISS)*CORE(NJ)+P(JSS)*CORE(NI))*G
      IF(NJ.LT.3) GO TO 110
      KINC=9
      JAP1=JA+1
      DO 100 K=JAP1,JB
      KC=(K*(K-1))/2
      DO 100 L=JA,K
      KL=KC+L
      BB=2.0
      IF(K.EQ.L) BB=1.0
      KK=KK+1
 100  E(N,3)=E(N,3)-P(KL)*CORE(NI)*BB*W(KK)
      GO TO 120
 110  KINC=0
 120  IF(NI.LT.3) GO TO 140
      IAP1=IA+1
      DO 130 I=IAP1,IB
      KA=(I*(I-1))/2
      DO 130 J=IA,I
      IJ=KA+J
      AA=2.0
      IF(I.EQ.J) AA=1.0
      KK=KK+1
      E(N,3)=E(N,3)-P(IJ)*CORE(NJ)*AA*W(KK)
 130  KK=KK+KINC
 140  CONTINUE
 150  N=N+1
C     COULOMB AND EXCHANGE TERMS
      N=1
      KK=0
      DO 170 II=2,NUMAT
      IA=NFIRST(II)
      IB=NLAST(II)
      IMINUS=II-1
      DO 160 JJ=1,IMINUS
      JA=NFIRST(JJ)
      JB=NLAST(JJ)
      N=N+1
      E(N,4)=0.0
      DO 160 I=IA,IB
      KA=(I*(I-1))/2
      DO 160 J=IA,I
      KB=(J*(J-1))/2
      IJ=KA+J
      AA=2.0
      IF(I.EQ.J) AA=1.0
      PIJ=P(IJ)
      DO 160 K=JA,JB
      KC=(K*(K-1))/2
      IK=KA+K
      JK=KB+K
      DO 160 L=JA,K
      IL=KA+L
      JL=KB+L
      KL=KC+L
      BB=2.0
      IF(K.EQ.L) BB=1.0
      KK=KK+1
      G=W(KK)
 160  E(N,4)=E(N,4)+AA*BB*G*PIJ*P(KL)
     .    -0.5*AA*BB*G*(ALPHA(IK)*ALPHA(JL)+ALPHA(IL)*ALPHA(JK)+
     .    BETA(IK)*BETA(JL)+BETA(IL)*BETA(JK))
 170  N=N+1
      XC=0.0
      XD=0.0
      WRITE(6,626)
 626  FORMAT(/,' TWO-CENTRE ENERGIES (EV)',//,3X,'E( A, B)',5X,
     .1HR,9X,1HN,9X,1HV,9X,1HE,8X,5HTOTAL)
      DO 220 I=1,NUMAT
      NI=NAT(I)
      XA=0.0
      XB=0.0
      IP1=I+1
      DO 210 J=IP1,NUMAT
      NJ=NAT(J)
      IJ=I+(J*(J-1))/2
      XT=E(IJ,1)+E(IJ,2)+E(IJ,3)+E(IJ,4)
      EABE=EABE+E(IJ,4)
      EABN=EABN+E(IJ,2)
      EABV=EABV+E(IJ,3)
      EABR=EABR+E(IJ,1)
      R=SQRT((COORD(1,I)-COORD(1,J))**2+(COORD(2,I)-COORD(2,J))**2+
     .  (COORD(3,I)-COORD(3,J))**2)
      IF(R.GT.1.9) GO TO 180
      NT=NI+NJ
      IF(NT.EQ.2) GO TO 180
      XB=XB+XT
      GO TO 190
 180  XA=XA+XT
 190  CONTINUE
 200  WRITE(6,627)I,J,(E(IJ,K),K=1,4),XT
 627  FORMAT(/,5X,I2,1X,I2,5F10.3)
 210  CONTINUE
      XC=XC+XA
      XD=XD+XB
 220  CONTINUE
      XT=XD+XC
      WRITE(6,628)XD,XC
 628  FORMAT(//,'  TOTAL SUM NEIGHBORING PAIR INTERACTIONS',10X,F15.4,
     .//,'  TOTAL SUM NONNEIGHBOR INTERACTIONS (R>1.9 A)',5X,F15.4)
      ET=EAU+EAE+EABR+EABV+EABN+EABE
      WRITE(6,629) TONE,XT
 629  FORMAT(//,'  TOTAL SUM ONE-CENTRE ENERGIES          ',5X,F15.4,
     .       //,'  TOTAL SUM TWO-CENTRE ENERGIES          ',5X,F15.4)
      WRITE(6,630)
 630  FORMAT(///,'  TOTAL SUMS OF ENERGY TERMS:')
      WRITE(6,631) EAU,EAE,EABV,EABR,EABE,EABN
 631  FORMAT(//,'  ONE-CENTRE CORE-ELECTRON ATTRACTION',8X,F15.4,
     .       //,'  ONE-CENTRE ELECTRON-ELECTRON REPULSION',5X,F15.4,
     .       //,'  TWO-CENTRE CORE-ELECTRON ATTRACTION',8X,F15.4,
     .       //,'  TWO-CENTRE CORE-ELECTRON RESONANCE ',8X,F15.4,
     .       //,'  TWO-CENTRE ELECTRON-ELECTRON REPULSION',5X,F15.4,
     .       //,'  TWO-CENTRE CORE-CORE REPULSION     ',8X,F15.4)
      WRITE(6,632) ET
 632  FORMAT(///,10X,'TOTAL ENERGY =',F15.4)
      IF(UHF) RETURN
      DO 250 I=1,LINEAR
 250  ALPHA(I)=P(I)
      RETURN
      END
